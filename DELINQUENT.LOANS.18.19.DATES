[ ---------------------------------------------------------------------
  Workfile: DELINQUENT.LOANS.18.19.DATES
  Revision: 1

   Purpose: To be able to view delinquent loans that are types 18&19.
     Notes: Copied from DELINQUENT.LOANS.2 and added select criteria for
  		 loan types 18&19.
  Rev Hist:
   6/15/2020 Created by Maria Otero
   11/18/2021 Edited by Connor Blakeney
			added CHARACTERREAD for due dates
  ----------------------------------------------------------------- ]

TARGET=LOAN

DEFINE
 DLOANS=CHARACTER(12) ARRAY(5,9999)
 DTOT=NUMBER ARRAY(5,5)   [ X = Days  Y = Grade ]
 DTOTBAL=MONEY ARRAY(5,5)
 DTOTINT=MONEY ARRAY(5,5)
 DTOTDEL=MONEY ARRAY(5,5)
 CATEGORY=NUMBER
 X=NUMBER
 Y=NUMBER
 PREVACCOUNTNUMBER=CHARACTER
 CATEDESC=CHARACTER ARRAY(6)

 CREDITGRADE=NUMBER
 NOTEGRADE=NUMBER
 GRADE=NUMBER
 GRADEDESCS=CHARACTER(4) ARRAY(5)
 STARTDATE=DATE
 ENDDATE=DATE
END
 
SETUP
 CATEDESC(0)="19 to 30 Days DQ"
 CATEDESC(1)="31 to 60 Days DQ"
 CATEDESC(2)="61 to 90 Days DQ"
 CATEDESC(3)="91 to 180 Days DQ"
 CATEDESC(4)="181 to 364 Days DQ"
 CATEDESC(5)="365 and Up Days DQ"
 CATEDESC(6)=""

 GRADEDESCS(0)="A+/A"
 GRADEDESCS(1)="B"
 GRADEDESCS(2)="C"
 GRADEDESCS(3)="D"
 GRADEDESCS(4)="E"
 GRADEDESCS(5)="*"

 X=0
 Y=0
 STARTDATE=DATEREAD("Enter start date:")
 ENDDATE=DATEREAD("Enter end date:")
END

SELECT
 ACCOUNT:NUMBER<"9000000000" AND
 ACCOUNT:CLOSEDATE='--/--/--' AND
 LOAN:BALANCE>$0.00 AND
 LOAN:CLOSEDATE='--/--/--' AND
 LOAN:CHARGEOFFDATE='--/--/--' AND
 ((SYSTEMDATE-LOAN:DUEDATE)>=19) AND
 (LOAN:TYPE=18 OR LOAN:TYPE=19)AND
 LOAN:DUEDATE > STARTDATE AND LOAN:DUEDATE < ENDDATE
END

SORT
 ACCOUNT:NUMBER
END

PRINT TITLE="DELINQUENT LOANS: TYPES 18&19" REPORTCATEGORY="BRCH OPS"
 HEADER=""
 SUPPRESSNEWLINE
 CALL GRADELOANS
 CALL CATEGORIZELOANS
END

TOTAL
 HEADERS
  PRINT CATEDESC(X)
  NEWLINE
  NEWLINE
 END
 FOR X=0 TO 5
 DO
   FOR Y=0 TO DTOT(X,5)-1
   DO
     CALL PRINTLOANDETAILS
     PREVACCOUNTNUMBER=SEGMENT(DLOANS(X,Y),1,10)
   END
   CALL PRINTLOANSUBTOTALS
 END
 CALL PRINTLOANGRANDTOTALS
 CALL PRINTGRADETOTALS
END

PROCEDURE GRADELOANS
 IF SEGMENT(LOAN:NOTENUMBER,1,2)="AA" THEN
  NOTEGRADE=0
 ELSE IF SEGMENT(LOAN:NOTENUMBER,1,1)="A" THEN
  NOTEGRADE=0
 ELSE IF SEGMENT(LOAN:NOTENUMBER,1,1)="B" THEN
  NOTEGRADE=1
 ELSE IF SEGMENT(LOAN:NOTENUMBER,1,1)="C" THEN
  NOTEGRADE=2
 ELSE IF SEGMENT(LOAN:NOTENUMBER,1,1)="D" THEN
  NOTEGRADE=3
 ELSE IF SEGMENT(LOAN:NOTENUMBER,1,1)="E" THEN
  NOTEGRADE=4
 ELSE
  NOTEGRADE=5

 IF LOAN:CREDITSCORE=0 THEN
  CREDITGRADE=5 
 ELSE IF LOAN:CREDITSCORE>=730 THEN
  CREDITGRADE=0
 ELSE IF LOAN:CREDITSCORE>=680 THEN
  CREDITGRADE=0
 ELSE IF LOAN:CREDITSCORE>=640 THEN
  CREDITGRADE=1
 ELSE IF LOAN:CREDITSCORE>=600 THEN
  CREDITGRADE=2
 ELSE IF LOAN:CREDITSCORE>=550 THEN
  CREDITGRADE=3
 ELSE
  CREDITGRADE=4

 IF NOTEGRADE=5 THEN
  IF CREDITGRADE=5 THEN GRADE=2
  ELSE GRADE=CREDITGRADE
 ELSE
  GRADE = NOTEGRADE
END

PROCEDURE CATEGORIZELOANS
 IF (SYSTEMDATE-LOAN:DUEDATE)>=19 AND (SYSTEMDATE-LOAN:DUEDATE)<=30        THEN CATEGORY=0
 ELSE IF (SYSTEMDATE-LOAN:DUEDATE)>=31 AND (SYSTEMDATE-LOAN:DUEDATE)<=60   THEN CATEGORY=1
 ELSE IF (SYSTEMDATE-LOAN:DUEDATE)>=61 AND (SYSTEMDATE-LOAN:DUEDATE)<=90   THEN CATEGORY=2
 ELSE IF (SYSTEMDATE-LOAN:DUEDATE)>=91 AND (SYSTEMDATE-LOAN:DUEDATE)<=180  THEN CATEGORY=3
 ELSE IF (SYSTEMDATE-LOAN:DUEDATE)>=181 AND (SYSTEMDATE-LOAN:DUEDATE)<=364 THEN CATEGORY=4
 ELSE CATEGORY=5                                                                     
 DLOANS(CATEGORY,DTOT(CATEGORY,5))=ACCOUNT:NUMBER+LOAN:ID
 DTOT(CATEGORY,GRADE)=DTOT(CATEGORY,GRADE)+1
 DTOT(CATEGORY,5)=DTOT(CATEGORY,5)+1
 DTOTBAL(CATEGORY,GRADE)=DTOTBAL(CATEGORY,GRADE)+LOAN:BALANCE
 DTOTINT(CATEGORY,GRADE)=DTOTINT(CATEGORY,GRADE)+LOAN:INTERESTDUE
 DTOTDEL(CATEGORY,GRADE)=DTOTDEL(CATEGORY,GRADE)+(LOAN:PAYMENT-LOAN:PARTIALPAYMENT)
END

PROCEDURE PRINTLOANDETAILS
 IF SEGMENT(DLOANS(X,Y),1,10)<>PREVACCOUNTNUMBER THEN
 DO
   FOR ACCOUNT SEGMENT(DLOANS(X,Y),1,10)
   DO
     COL=001 REPEATCHR("-",132)
     NEWLINE
     FOR EACH NAME WITH NAME:TYPE=00
     DO
       COL=001 ACCOUNT:NUMBER
       COL=012 NAME:SHORTNAME
       COL=043 "Ty: "
       COL=047 NAME:TYPE
       COL=053 "SSN: "
       COL=072 RIGHT SEGMENT(NAME:SSN,1,3)+"-"+SEGMENT(NAME:SSN,4,5)+"-"+SEGMENT(NAME:SSN,6,9)
       COL=075 "Home: "
       COL=094 RIGHT NAME:HOMEPHONE
       COL=097 "Work: "
       COL=118 RIGHT NAME:WORKPHONE
       COL=121 "Acct Br:"
       COL=131 FORMAT("#9",ACCOUNT:BRANCH)
       NEWLINE
     END
     FOR EACH SHARE WITH SHARE:CLOSEDATE='--/--/--'
     DO
       COL=003 "S"
       COL=005 SHARE:ID
       COL=012 SHARE:DESCRIPTION
       COL=043 "Type:"
       COL=051 SHARE:TYPE
       COL=053 "Bal:"
       COL=072 SHARE:BALANCE
       COL=075 "Avail:"
       COL=094 SHARE:AVAILABLEBALANCE
       NEWLINE
     END
     FOR EACH EFT WITH EFT:EXPIRATIONDATE='--/--/--' AND (EFT:TYPE=0 OR EFT:TYPE=2)
     DO
       IF EFT:TYPE=0 THEN COL=003 "P"
       ELSE COL=003 "A"
       COL=011 RIGHT EFT:GROUPNUMBER
       COL=053 "Curr:"
       COL=072 EFT:POSTAMOUNT
       COL=075 "Stand:"
       COL=094 EFT:STANDARDAMOUNT
       NEWLINE
     END
     FOR EACH LOAN WITH LOAN:BALANCE>$0.00 AND
     LOAN:CLOSEDATE='--/--/--' AND
     LOAN:CHARGEOFFDATE='--/--/--' AND
     ((SYSTEMDATE-LOAN:DUEDATE)>=19) AND
     LOAN:ID=SEGMENT(DLOANS(X,Y),11,12)
     DO
       COL=003 "L"
       COL=005 LOAN:ID
       COL=012 LOAN:DESCRIPTION
       COL=043 "Type:"
       COL=051 LOAN:TYPE
       COL=053 "Bal:"
       COL=072 LOAN:BALANCE
       COL=075 "Avail:"
       COL=094 LOAN:AVAILABLECREDIT
       COL=097 "Note: "
       COL=113 RIGHT LOAN:NOTENUMBER
       COL=115 "Int Rate:"
       COL=132 LOAN:INTERESTRATE
       NEWLINE
       COL=005 "Due:"
       COL=018 RIGHT LOAN:DUEDATE
       COL=020 "Days:"
       COL=030 RIGHT (SYSTEMDATE-LOAN:DUEDATE)
       COL=033 "DQ Amt:"
       COL=050 (LOAN:PAYMENT-LOAN:PARTIALPAYMENT)
       COL=053 "Orig Bal:"
       COL=072 LOAN:LRLNBALANCE
       COL=075 "Payment:"
       COL=094 LOAN:PAYMENT
       COL=097 "Credit Score:"
       COL=113 RIGHT LOAN:CREDITSCORE
       COL=115 "Maturity:"
       COL=132 RIGHT LOAN:MATURITYDATE   
       NEWLINE
       COL=005 "Pmt:"
       COL=018 RIGHT LOAN:LASTPAYMENTDATE
       COL=020 "Days:"
       COL=030 RIGHT (SYSTEMDATE-LOAN:LASTPAYMENTDATE)
       COL=033 "IntDue:"
       COL=050 LOAN:INTERESTDUE
       COL=053 "OrigDate:"
       COL=072 RIGHT LOAN:LRLNDATE
       COL=075 "Partial:"
       COL=094 LOAN:PARTIALPAYMENT
       COL=097 "Appr Code:"
       COL=113 RIGHT LOAN:APPROVALCODE
       NEWLINE
       DTOTBAL(X,5)=DTOTBAL(X,5)+LOAN:BALANCE
       DTOTINT(X,5)=DTOTINT(X,5)+LOAN:INTERESTDUE
       DTOTDEL(X,5)=DTOTDEL(X,5)+(LOAN:PAYMENT-LOAN:PARTIALPAYMENT)
     END
   END
 END
 ELSE
 DO
   FOR ACCOUNT SEGMENT(DLOANS(X,Y),1,10)
   DO
     FOR EACH LOAN WITH LOAN:BALANCE>$0.00 AND
     LOAN:CLOSEDATE='--/--/--' AND
     LOAN:CHARGEOFFDATE='--/--/--' AND
     ((SYSTEMDATE-LOAN:DUEDATE)>=19) AND
     LOAN:ID=SEGMENT(DLOANS(X,Y),11,12)
     DO
       COL=003 "L"
       COL=005 LOAN:ID
       COL=012 LOAN:DESCRIPTION
       COL=043 "Type:"
       COL=051 LOAN:TYPE
       COL=053 "Bal:"
       COL=072 LOAN:BALANCE
       COL=075 "Avail:"
       COL=094 LOAN:AVAILABLECREDIT
       COL=097 "Note: "
       COL=113 RIGHT LOAN:NOTENUMBER
       COL=115 "Int Rate:"
       COL=132 LOAN:INTERESTRATE
       NEWLINE
       COL=005 "Due:"
       COL=018 RIGHT LOAN:DUEDATE
       COL=020 "Days:"
       COL=030 RIGHT (SYSTEMDATE-LOAN:DUEDATE)
       COL=033 "DQ Amt:"
       COL=050 (LOAN:PAYMENT-LOAN:PARTIALPAYMENT)
       COL=053 "Orig Bal:"
       COL=072 LOAN:LRLNBALANCE
       COL=075 "Payment:"
       COL=094 LOAN:PAYMENT
       COL=097 "Credit Score:"
       COL=113 RIGHT LOAN:CREDITSCORE
       COL=115 "Maturity:"
       COL=132 RIGHT LOAN:MATURITYDATE   
       NEWLINE
       COL=005 "Pmt:"
       COL=018 RIGHT LOAN:LASTPAYMENTDATE
       COL=020 "Days:"
       COL=030 RIGHT (SYSTEMDATE-LOAN:LASTPAYMENTDATE)
       COL=033 "IntDue:"
       COL=050 LOAN:INTERESTDUE
       COL=053 "OrigDate:"
       COL=072 RIGHT LOAN:LRLNDATE
       COL=075 "Partial:"
       COL=094 LOAN:PARTIALPAYMENT
       COL=097 "Appr Code:"
       COL=113 RIGHT LOAN:APPROVALCODE
       NEWLINE
       DTOTBAL(X,5)=DTOTBAL(X,5)+LOAN:BALANCE
       DTOTINT(X,5)=DTOTINT(X,5)+LOAN:INTERESTDUE
       DTOTDEL(X,5)=DTOTDEL(X,5)+(LOAN:PAYMENT-LOAN:PARTIALPAYMENT)
     END
   END
 END
END

PROCEDURE PRINTLOANSUBTOTALS
 COL=001 REPEATCHR("-",132)
 NEWLINE
 PRINT "Total for Category  Count               Balance          Interest Due            Delinquent"
 NEWLINE
 COL=001 CATEDESC(X)
 COL=025 RIGHT DTOT(X,5)
 COL=047 RIGHT DTOTBAL(X,5)
 COL=069 RIGHT DTOTINT(X,5)
 COL=091 RIGHT DTOTDEL(X,5)
 NEWPAGE
END

PROCEDURE PRINTLOANGRANDTOTALS
 PRINT "Total for Category  Count               Balance          Interest Due            Delinquent"
 NEWLINE
 COL=001 REPEATCHR("-",132)
 NEWLINE

 FOR X=0 TO 5
 DO
   COL=001 CATEDESC(X)
   COL=025 RIGHT DTOT(X,5)
   COL=047 RIGHT DTOTBAL(X,5)
   COL=069 RIGHT DTOTINT(X,5)
   COL=091 RIGHT DTOTDEL(X,5)
   NEWLINE
 END
 COL=001 REPEATCHR("-",132)
 NEWLINE
 COL=001 "Total"
 COL=025 RIGHT DTOT(0,5)+DTOT(1,5)+DTOT(2,5)+DTOT(3,5)+DTOT(4,5)+DTOT(5,5)
 COL=047 RIGHT DTOTBAL(0,5)+DTOTBAL(1,5)+DTOTBAL(2,5)+DTOTBAL(3,5)+DTOTBAL(4,5)+DTOTBAL(5,5)
 COL=069 RIGHT DTOTINT(0,5)+DTOTINT(1,5)+DTOTINT(2,5)+DTOTINT(3,5)+DTOTINT(4,5)+DTOTINT(5,5)
 COL=091 RIGHT DTOTDEL(0,5)+DTOTDEL(1,5)+DTOTDEL(2,5)+DTOTDEL(3,5)+DTOTDEL(4,5)+DTOTDEL(5,5)
 NEWLINE
 NEWLINE
END

PROCEDURE PRINTGRADETOTALS
 FOR Y=0 TO 4
 DO
   PRINT "Loan Grade "
   PRINT GRADEDESCS(Y)
   NEWLINE
   PRINT "Total for Category  Count               Balance          Interest Due            Delinquent"
   NEWLINE
   COL=001 REPEATCHR("-",132)
   NEWLINE

   FOR X=0 TO 5
   DO
     COL=001 CATEDESC(X)
     COL=025 RIGHT DTOT(X,Y)
     COL=047 RIGHT DTOTBAL(X,Y)
     COL=069 RIGHT DTOTINT(X,Y)
     COL=091 RIGHT DTOTDEL(X,Y)
     NEWLINE
   END
   COL=001 REPEATCHR("-",132)
   NEWLINE
   COL=001 "Total"
   COL=025 RIGHT DTOT(0,Y)+DTOT(1,Y)+DTOT(2,Y)+DTOT(3,Y)+DTOT(4,Y)+DTOT(5,Y)
   COL=047 RIGHT DTOTBAL(0,Y)+DTOTBAL(1,Y)+DTOTBAL(2,Y)+DTOTBAL(3,Y)+DTOTBAL(4,Y)+DTOTBAL(5,Y)
   COL=069 RIGHT DTOTINT(0,Y)+DTOTINT(1,Y)+DTOTINT(2,Y)+DTOTINT(3,Y)+DTOTINT(4,Y)+DTOTINT(5,Y)
   COL=091 RIGHT DTOTDEL(0,Y)+DTOTDEL(1,Y)+DTOTDEL(2,Y)+DTOTDEL(3,Y)+DTOTDEL(4,Y)+DTOTDEL(5,Y)
   NEWLINE
   NEWLINE
 END
END






